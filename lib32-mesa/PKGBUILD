# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa
pkgname=(
  'lib32-vulkan-mesa-layers'
  'lib32-opencl-clover-mesa'
  'lib32-opencl-rusticl-mesa'
  'lib32-vulkan-intel'
  'lib32-vulkan-nouveau'
  'lib32-vulkan-radeon'
  'lib32-vulkan-swrast'
  'lib32-libva-mesa-driver'
  'lib32-mesa-vdpau'
  'lib32-mesa'
)
_mesaver=24.2.0-rc2
pkgver=${_mesaver//-/.}.chos1
pkgrel=1
epoch=1
pkgdesc="Open-source OpenGL drivers - 32-bit"
url="https://www.mesa3d.org/"
arch=('x86_64')
license=('MIT AND BSD-3-Clause AND SGI-B-2.0')
makedepends=(
  'lib32-clang'
  'lib32-expat'
  'lib32-gcc-libs'
  'lib32-glibc'
  'lib32-libdrm'
  'lib32-libelf'
  'lib32-libglvnd'
  'lib32-libva'
  'lib32-libvdpau'
  'lib32-libx11'
  'lib32-libxcb'
  'lib32-libxext'
  'lib32-libxfixes'
  'lib32-libxml2'
  'lib32-libxrandr'
  'lib32-libxshmfence'
  'lib32-libxxf86vm'
  'lib32-llvm'
  'lib32-llvm-libs'
  'lib32-lm_sensors'
  'lib32-rust-libs'
  'lib32-spirv-llvm-translator'
  'lib32-spirv-tools'
  'lib32-systemd'
  'lib32-vulkan-icd-loader'
  'lib32-wayland'
  'lib32-xcb-util-keysyms'
  'lib32-zlib'
  'lib32-zstd'

  # shared between mesa and lib32-mesa
  'cbindgen'
  'clang'
  'cmake'
  'elfutils'
  'glslang'
  'libclc'
  'meson'
  'python-mako'
  'python-packaging'
  'python-ply'
  'python-yaml'
  'rust-bindgen'
  'wayland-protocols'
  'xorgproto'
)
options=(
  # GCC 14 LTO causes segfault in LLVM under si_llvm_optimize_module
  !lto
)
source=(
  https://mesa.freedesktop.org/archive/mesa-${_mesaver}.tar.xz{,.sig}
  LICENSE
  0001-STEAMOS-Dynamic-swapchain-override-for-gamescope-lim.patch
  0002-Nouveau-load-zink-as-the-GL-driver.patch
)
validpgpkeys=(
  '8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D'  # Emil Velikov <emil.l.velikov@gmail.com>
  '946D09B5E4C9845E63075FF1D961C596A7203456'  # Andres Gomez <tanty@igalia.com>
  'E3E8F480C52ADD73B278EE78E1ECBE07D7D70895'  # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>
  'A5CC9FEC93F2F837CB044912336909B6B25FADFA'  # Juan A. Suarez Romero <jasuarez@igalia.com>
  '71C4B75620BC75708B4BDB254C95FAAB3EB073EC'  # Dylan Baker <dylan@pnwbakers.com>
  '57551DE15B968F6341C248F68D8E31AFC32428A6'  # Eric Engestrom <eric@engestrom.ch>
)

# Rust crates for NVK, used as Meson subprojects
declare -A _crates=(
   proc-macro2    1.0.86
   quote          1.0.33
   syn            2.0.68
   unicode-ident  1.0.12
   paste          1.0.14
)

for _crate in "${!_crates[@]}"; do
  source+=($_crate-${_crates[$_crate]}.tar.gz::https://crates.io/api/v1/crates/$_crate/${_crates[$_crate]}/download)
done

sha256sums=('3b2207cb4aadf2c2f08be0e62a10b17f7638786268e74e9cd8a92f176630b9d0'
            'SKIP'
            '7052ba73bb07ea78873a2431ee4e828f4e72bda7d176d07f770fa48373dec537'
            'f1343e521907006dc482e619dff5b43db616977febd9ba80af8ef7511da5b6b5'
            '1727a770f7d9c1838718a186545ae84fad05e29be18043f8eec46e475842a790'
            '5e719e8df665df0d1c8fbfd238015744736151d4445ec0836b8e628aae103b77'
            '3354b9ac3fae1ff6755cb6db53683adb661634f67557942dea4facebec0fee4b'
            '5267fca4496028628a95160fc423a33e8b2e6af8a5302579e322e4b520293cae'
            'de3145af08024dea9fa9914f381a17b8fc6034dfb00f3a84013f7ff43f29ed4c'
            '901fa70d88b9d6c98022e23b4136f9f3e54e4662c3bc1bd1d84a42a9a0f0c1e9')
b2sums=('e0864a0b323e3388cc8f138c0ecd9751b2997a6dc2aeb3c7bf110b51faf7f93473278a27862588b5a7272c3281924e2d049e86171d7ce78a1382f5b436b83b96'
        'SKIP'
        '1ecf007b82260710a7bf5048f47dd5d600c168824c02c595af654632326536a6527fbe0738670ee7b921dd85a70425108e0f471ba85a8e1ca47d294ad74b4adb'
        'be2f4cc356e67861b7c47b15132ad3178bfbb206137223cf77691fc5e4d75cb035a3a74cdbfa3766cc2924dfd503417f4758084732215264c715f050f231bcc8'
        'bfb6a0ef05abedecc5fc550a8de9c57f2909e0307cec963c913c387a9f037aa88acc588ac3279e5869033ffac7949c5867c96f30763d521659d62047cd58d6d8'
        '4b89e07f23af8328dbb34fe2b3f1b202f1e6a3885a6269740a23359b41bb4099ac2484565d3b2b0936261689ca525785ac620c766997234fd8d0f409e80e5ea3'
        '4cede03c08758ccd6bf53a0d0057d7542dfdd0c93d342e89f3b90460be85518a9fd24958d8b1da2b5a09b5ddbee8a4263982194158e171c2bba3e394d88d6dac'
        '77c4b166f1200e1ee2ab94a5014acd334c1fe4b7d72851d73768d491c56c6779a0882a304c1f30c88732a6168351f0f786b10516ae537cff993892a749175848'
        '35e8548611c51ee75f4d04926149e5e54870d7073d9b635d550a6fa0f85891f57f326bdbcff3dd8618cf40f8e08cf903ef87d9c034d5921d8b91e1db842cdd7c'
        '8bc6f68ed286bea617a2cfaf3949bb699d3a0466faeca735314a51596ce950e4ee57eda88154bd562c1728cfaff4cdb5bc1ba701b9d47a9c50d4c4f011bee975')

prepare() {
  cd mesa-$_mesaver

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  # Include package release in version string so Chromium invalidates
  # its GPU cache; otherwise it can cause pages to render incorrectly.
  # https://bugs.launchpad.net/ubuntu/+source/chromium-browser/+bug/2020604
  echo "$pkgver-$epoch.$pkgrel" >VERSION
}

build() {
  local meson_options=(
    --cross-file lib32
    -D android-libbacktrace=disabled
    -D b_ndebug=true
    -D gallium-drivers=radeonsi,nouveau,swrast,i915,iris,crocus,zink
    -D gallium-extra-hud=true
    -D gallium-nine=true
    -D gallium-omx=disabled
    -D gallium-opencl=icd
    -D gallium-rusticl=true
    -D gallium-xa=disabled
    -D gles1=disabled
    -D glvnd=enabled
    -D glx=dri
    -D intel-clc=enabled
    -D intel-rt=disabled
    -D libunwind=disabled
    -D microsoft-clc=disabled
    -D osmesa=true
    -D platforms=x11,wayland
    -D valgrind=disabled
    -D video-codecs=all
    -D vulkan-drivers=amd,intel,intel_hasvk,swrast,nouveau
    -D vulkan-layers=device-select,intel-nullhw,overlay
  )

  # Build only minimal debug info to reduce size
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  # Inject subproject packages
  export MESON_PACKAGE_CACHE_DIR="$srcdir"

  export BINDGEN_EXTRA_CLANG_ARGS="-m32"

  arch-meson mesa-$_mesaver build "${meson_options[@]}"
  meson configure build --no-pager # Print config
  meson compile -C build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  meson install -C build --destdir "$srcdir/fakeinstall"
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

_libdir=usr/lib32

package_lib32-vulkan-mesa-layers() {
  pkgdesc="Mesa's Vulkan layers - 32-bit"
  depends=(
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libxcb'
    'lib32-wayland'

    'vulkan-mesa-layers'
  )
  conflicts=('lib32-vulkan-mesa-layer')
  replaces=('lib32-vulkan-mesa-layer')

  rm -rv fakeinstall/usr/share/vulkan/explicit_layer.d
  rm -rv fakeinstall/usr/share/vulkan/implicit_layer.d
  _install fakeinstall/$_libdir/libVkLayer_*.so
  rm -v fakeinstall/usr/bin/mesa-overlay-control.py

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-opencl-clover-mesa() {
  pkgdesc="Open-source OpenCL drivers - Clover variant - 32-bit"
  depends=(
    'lib32-clang'
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libelf'
    'lib32-llvm-libs'
    'lib32-spirv-llvm-translator'
    'lib32-spirv-tools'
    'lib32-zlib'
    'lib32-zstd'

    'opencl-clover-mesa'
  )
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=('lib32-opencl-driver')
  replaces=("lib32-opencl-mesa<=23.1.4-1")
  conflicts=('lib32-opencl-mesa')

  rm -v fakeinstall/etc/OpenCL/vendors/mesa.icd
  _install fakeinstall/$_libdir/libMesaOpenCL*
  _install fakeinstall/$_libdir/gallium-pipe

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-opencl-rusticl-mesa() {
  pkgdesc="Open-source OpenCL drivers - RustICL variant - 32-bit"
  depends=(
    'lib32-clang'
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libelf'
    'lib32-llvm-libs'
    'lib32-spirv-llvm-translator'
    'lib32-spirv-tools'
    'lib32-zlib'
    'lib32-zstd'

    'opencl-rusticl-mesa'
  )
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=('lib32-opencl-driver')
  replaces=("lib32-opencl-mesa<=23.1.4-1")
  conflicts=('lib32-opencl-mesa')

  rm -v fakeinstall/etc/OpenCL/vendors/rusticl.icd
  _install fakeinstall/$_libdir/libRusticlOpenCL*

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-vulkan-intel() {
  pkgdesc="Open-source Vulkan driver for Intel GPUs - 32-bit"
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxshmfence'
    'lib32-systemd'
    'lib32-vulkan-icd-loader'
    'lib32-wayland'
    'lib32-xcb-util-keysyms'
    'lib32-zlib'
    'lib32-zstd'

    'vulkan-intel'
  )
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  provides=('lib32-vulkan-driver')

  _install fakeinstall/usr/share/vulkan/icd.d/intel_*.json
  _install fakeinstall/$_libdir/libvulkan_intel*.so

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-vulkan-nouveau() {
  pkgdesc="Open-source Vulkan driver for Nvidia GPUs - 32-bit"
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxshmfence'
    'lib32-systemd'
    'lib32-vulkan-icd-loader'
    'lib32-wayland'
    'lib32-xcb-util-keysyms'
    'lib32-zlib'
    'lib32-zstd'

    'vulkan-nouveau'
  )
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  provides=('lib32-vulkan-driver')

  _install fakeinstall/usr/share/vulkan/icd.d/nouveau_*.json
  _install fakeinstall/$_libdir/libvulkan_nouveau*.so

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-vulkan-radeon() {
  pkgdesc="Open-source Vulkan driver for AMD GPUs - 32-bit"
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libelf'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxshmfence'
    'lib32-llvm-libs'
    'lib32-systemd'
    'lib32-vulkan-icd-loader'
    'lib32-wayland'
    'lib32-xcb-util-keysyms'
    'lib32-zlib'
    'lib32-zstd'

    'vulkan-radeon'
  )
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  provides=('lib32-vulkan-driver')

  rm -v fakeinstall/usr/share/drirc.d/00-radv-defaults.conf
  _install fakeinstall/usr/share/vulkan/icd.d/radeon_icd*.json
  _install fakeinstall/$_libdir/libvulkan_radeon.so

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-vulkan-swrast() {
  pkgdesc="Open-source Vulkan driver for CPUs (Software Rasterizer) - 32-bit"
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxshmfence'
    'lib32-llvm-libs'
    'lib32-systemd'
    'lib32-vulkan-icd-loader'
    'lib32-wayland'
    'lib32-xcb-util-keysyms'
    'lib32-zlib'
    'lib32-zstd'

    'vulkan-swrast'
  )
  optdepends=('lib32-vulkan-mesa-layers: additional vulkan layers')
  conflicts=('lib32-vulkan-mesa')
  replaces=('lib32-vulkan-mesa')
  provides=('lib32-vulkan-driver')

  _install fakeinstall/usr/share/vulkan/icd.d/lvp_icd*.json
  _install fakeinstall/$_libdir/libvulkan_lvp.so

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-libva-mesa-driver() {
  pkgdesc="Open-source VA-API drivers - 32-bit"
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libelf'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxshmfence'
    'lib32-llvm-libs'
    'lib32-zlib'
    'lib32-zstd'

    'libva-mesa-driver'
  )
  provides=('lib32-libva-driver')

  _install fakeinstall/$_libdir/dri/*_drv_video.so

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-mesa-vdpau() {
  pkgdesc="Open-source VDPAU drivers - 32-bit"
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libelf'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxshmfence'
    'lib32-llvm-libs'
    'lib32-zlib'
    'lib32-zstd'

    'mesa-vdpau'
  )
  provides=('lib32-vdpau-driver')

  _install fakeinstall/$_libdir/vdpau

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-mesa() {
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
    'lib32-glibc'
    'lib32-libdrm'
    'lib32-libelf'
    'lib32-libglvnd'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-libxext'
    'lib32-libxfixes'
    'lib32-libxshmfence'
    'lib32-libxxf86vm'
    'lib32-llvm-libs'
    'lib32-lm_sensors'
    'lib32-wayland'
    'lib32-zlib'
    'lib32-zstd'

    'mesa'
  )
  optdepends=(
    'opengl-man-pages: for the OpenGL API man pages'
  )
  provides=(
    'lib32-mesa-libgl'
    'lib32-opengl-driver'
  )
  conflicts=('lib32-mesa-libgl')
  replaces=('lib32-mesa-libgl')

  rm -v fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf
  rm -v fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json

  # ati-dri, nouveau-dri, intel-dri, svga-dri, swrast, swr
  _install fakeinstall/$_libdir/dri/*_dri.so

  _install fakeinstall/$_libdir/d3d
  _install fakeinstall/$_libdir/lib{gbm,glapi}.so*
  _install fakeinstall/$_libdir/libOSMesa.so*

  rm -rv fakeinstall/usr/include
  _install fakeinstall/$_libdir/pkgconfig

  # libglvnd support
  _install fakeinstall/$_libdir/libGLX_mesa.so*
  _install fakeinstall/$_libdir/libEGL_mesa.so*

  # gallium so file
  _install fakeinstall/$_libdir/libgallium-*.so

  # indirect rendering
  ln -sr "$pkgdir"/$_libdir/libGLX_{mesa,indirect}.so.0

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -Dm644 mesa-$_mesaver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
